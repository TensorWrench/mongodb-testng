/*
Copyright 2013 TensorWrench, LLC 

Licensed under the Apache License, Version 2.0 (the "License"); 
you may not use this file except in compliance with the License. 
You may obtain a copy of the License at 

http://www.apache.org/licenses/LICENSE-2.0 

Unless required by applicable law or agreed to in writing, software 
distributed under the License is distributed on an "AS IS" BASIS, 
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. 
See the License for the specific language governing permissions and 
limitations under the License.
*/
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'pmd'
apply plugin: 'gitrelease'
apply from:"file:src/gradle/emma.gradle"
apply from:"file:src/gradle/backgroundTask.gradle"
apply plugin: 'maven'

//===============================================
// Project settings
//===============================================
version = '1.0-SNAPSHOT'
group 'com.tensorwrench.testng'
sourceCompatibility = 1.7

ext {
  mongoDir=new File("$buildDir/mongo-data");
	testngVersion=6.8
	mongoDriverVersion="2.11.+"
}


//===============================================
// Build Tools
//===============================================
buildscript {
	repositories { 
		mavenCentral() 
	}  
	dependencies {
    classpath 'no.entitas.gradle:gradle-release-plugin:1.16'
  }
}

//===============================================
// Dependency info
//===============================================
repositories {
	mavenLocal()
  mavenCentral()
}

dependencies {
	compile(
			"org.mongodb:mongo-java-driver:${mongoDriverVersion}"
			, "org.testng:testng:${testngVersion}"
  )		
	
	emma "emma:emma:2.0.5312", "emma:emma_ant:2.0.5312"
}

release {
	failOnSnapshotDependencies = true
	versionStrategy = { currentVersion ->
			if (System.properties['release.version']) {
					System.properties['release.version']
			} else {
					new BigDecimal(currentVersion).add(BigDecimal.ONE).toPlainString()
			}
	}
	startVersion = { currentBranch -> "1" }
}
//===============================================
// Project Overhead
//===============================================
task wrapper(type: Wrapper) {
	gradleVersion="1.6"
}

eclipse.classpath {
	downloadJavadoc = true
	downloadSources = true
	defaultOutputDir = file("$buildDir/build-eclipse")
}

//===============================================
// Documentation
//===============================================

javadoc {
	source=sourceSets.main.allJava
	destinationDir = reporting.file("javadocs")
}

//===============================================
// Testing
//===============================================
test {
	useTestNG()
}
test.dependsOn "startMongoDB"

//==========================================================================
// Artifact Information
//==========================================================================
jar {
	manifest {
		attributes 'Title': 'shiro-mongodb-realm',
				       'Version': version
	}
}

task sourcesJar(type: Jar, dependsOn:classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives(sourcesJar)
	archives(javadocJar)
}

uploadArchives {
    repositories {
       mavenDeployer {
           repository(url: new File("${buildDir}/mvnRepo").toURI())
					 pom.groupId = project.group
					 pom.project {
						 packaging "jar"
						 name "MongoDB TestNG"
						 description "TestNG support for populating a MongoDB databse before a test is run"
						 url "https://github.com/TensorWrench/mongodb-testng"
						 scm {
						 	 connection "scm:git:https://github.com/TensorWrench/mongodb-testng"
							 url "https://github.com/TensorWrench/mongodb-testng"
						 }
						 developers {
							 developer {
								 id "jason"
								 name "Jason Wagner"
								 organization "TensorWrench, LLC"
							 }
						 }
						 licenses {
								 license {
										 name 'The Apache Software License, Version 2.0'
										 url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
										 distribution 'repo'
								 }
						 }
				 }
			 }
    }
}

//==========================================================================
// Setup mongodb

backgroundTask {
	processName="MongoDB"
	group='Mongo'
	commandLine=["mongod","--dbpath",mongoDir.getCanonicalPath()];
	workDir=mongoDir
}
